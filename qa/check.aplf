 ok←check ok;⎕IO;lc;fn;code;nq;np;im;names;valid;syms;sym;nc;val;shape;rank;subst;qt;id;i;seed;rand;type;deparen;dlb;m
 :If {0::0 ⋄ ⍎⍵}'StopIfTestFails'
     :If ok
         {((⎕stop ⍵)~⎕LC[2])⎕STOP⍵}(2⊃⎕NSI),'.',2⊃⎕SI  ⍝ be sure the stop we would set if this test failed is removed (keeping others which the user mayn have wanted!)
     :Else
         (⎕LC[2]+1)⎕STOP(2⊃⎕NSI),'.',2⊃⎕SI  ⍝ set stop in calling fn in the line following the one that called us...
     :EndIf
 :EndIf
 →ok⍴0

 ⍝ Format the failing check to make it easy to repro

 ⎕IO←1
 fn lc←2⊃¨⎕XSI ⎕LC
 code←(⎕CR fn)[1+lc;]
 code←(4+1⍳⍨'check'⍷code)↓code
 nq←0=≠\code=''''             ⍝ Not quoted
 code←code↑⍨¯1+(nq∧code='⍝')⍳1
 nq←(≢code)↑nq
 names←nq∧code∊valid←'⎕∆⍙_',⎕D,∊¯1 1 ⎕C¨⊂⎕A
 m←2</names
 nc←⎕NC syms←∪{(¯1≠⎕NC ⍵)/⍵}((m⊂names)/¨m⊂code)∩¨⊂valid
 rand←0⍴⊂''

 :For sym :In syms ⍝ try to replace each sym with something more useful
     :Trap 999
         subst←''
         ⎕EX'val' ⋄ val←1(86⌶)sym
         :If 2=⎕NC'val'
             rank←≢shape←⍴val
             :Select rank
             :Case 0 ⋄ qt←((⎕DR val)∊80 82)/'''' ⋄ subst←qt,(⍕val),qt
             :Case 1
                 :If (0=1↑0⍴val)∧10≥≢val
                     val←(⍕val),(0=≢val)/'⍬'
                 :EndIf
             :EndSelect

             :If (0=≢subst)∧('rand'≡4↑sym)∧(1↑1⊃⎕VFI 4↓sym) ⍝ 'randN'
                 id←2⊃⎕VFI 4⊃sym
             :AndIf (≢SEEDS)≥i←(⊃¨SEEDS)⍳id
                 (id type shape seed)←i⊃SEEDS
                 rand,←⊂sym,'←',(⍕⊃seed),' RandomArray 0 ',(⍕type),' (',(⍕shape),')'
             :EndIf
         :EndIf
     :EndTrap
     :If 0≠≢subst
         code←(sym ⎕R subst)code
     :EndIf
 :EndFor

 nq←0=≠\code=''''             ⍝ Not quoted
 np←0=+\nq×1 ¯1 0['()'⍳code]  ⍝ Not parenthesised
 im←1⍳⍨np∧code='≡'            ⍝ 1st ≡ symbol
 :If ' '∧.=im↓code
     im←1⍳⍨np∧code='='            ⍝ No ≡, try =
 :AndIf ' '∧.=im↓code
     'No ≡ or = found'⎕SIGNAL 11
 :EndIf

 deparen←{')('≡2↑¯1⌽⍵:1↓¯1↓⍵ ⋄ ⍵}
 dlb←{(+/∧\⍵=' ')↓⍵}

 ⎕←'*** test failed at ',fn,'[',(⍕lc),'] ***'
 ⎕←'Result of . . :    ',deparen dlb(im-1)↑code
 ⎕←'Did not match :    ',deparen dlb{(-+/∧\' '=⌽⍵)↓⍵}im↓code
 :If 0≠⍴rand
     ⎕←((≢rand)↑⊂'   ... where :  '),⍪rand
 :EndIf
 ⎕←''
